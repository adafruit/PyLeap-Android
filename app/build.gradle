plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.adafruit.pyleap'
    compileSdk 33

    defaultConfig {
        applicationId "com.adafruit.pyleap"
        minSdk 27
        targetSdk 33
        versionCode 5
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-opt-in=kotlin.RequiresOptIn",
                "-Xjvm-default=enable"      // Needed for accompanist-webview
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Core
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    // Material design
    implementation 'androidx.compose.material3:material3:1.1.0'
    implementation "androidx.compose.material3:material3-window-size-class:1.1.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    // Material icons
    implementation "androidx.compose.material:material-icons-extended:1.4.3"

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.5.3'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Coil
    implementation("io.coil-kt:coil-compose:2.1.0")

    // WebView
    implementation "com.google.accompanist:accompanist-webview:$accompanist_version"
    implementation 'androidx.startup:startup-runtime:1.1.1'

    // Permissions
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"

    // FileTransfer lib
    implementation project(path: ':libraries:filetransfer')

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"


    // Fix kotlin-stdlib duplicate class errors: https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

}